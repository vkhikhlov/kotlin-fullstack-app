apply plugin: 'kotlin2js'
apply plugin: 'org.jetbrains.kotlin.frontend'

buildscript {

    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://dl.bintray.com/kotlin/kotlin-eap"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-frontend-plugin:$kotlinFrontendPluginVersion"
    }
}

sourceSets {
    main.kotlin.srcDirs += 'src/main/kotlin'
}

compileKotlin2Js {
    kotlinOptions {
        metaInfo = true
        moduleKind = 'commonjs'
        outputFile = "$projectDir/build/classes/main/${project.name}.js"
        if (!production) {
            sourceMap = true
            sourceMapEmbedSources = "always"
            suppressWarnings = true
        }
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        url "https://dl.bintray.com/kotlin/kotlin-eap"
    }
    maven {
        url "https://dl.bintray.com/vkhikhlov/kotlin-vue-wrappers"
    }
//    flatDir {
//        dirs(
//                '../../kotlin-vue-wrappers/kotlin-vue/build/libs',
//                '../../kotlin-vue-wrappers/kotlin-vue-extensions/build/libs'
//        )
//    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlinVersion"
    compile "org.jetbrains.kotlinx:kotlinx-html-js:$kotlinxHtmlVersion"
    compile "com.github.vkhikhlov:kotlin-vue:0.0.4"
//    compile "com.github.vkhikhlov:kotlin-vue-extensions:0.0.2"
}

kotlin {
    experimental {
        coroutines 'enable'
    }
}


kotlinFrontend {
    downloadNodeJsVersion = "latest"
    if (!production) {
        sourceMaps = true
    }

    npm {
        dependency("vue", "latest")
        dependency("jquery", "latest")
        dependency("semantic-ui", "latest")

        devDependency('css-loader', 'latest')
        devDependency('style-loader', 'latest')
        devDependency('file-loader', 'latest')
        devDependency("html-webpack-plugin", "latest")
        devDependency("webpack", "latest")
        devDependency("webpack-dev-server", "latest")
    }

    webpackBundle {
        bundleName = "main"
        contentPath = file('src/main/resources')
        publicPath = (production.toBoolean()) ? '/static/' : '/'
        port = 8088
        mode = production.toBoolean() ? 'production' : 'development'
    }

    define('PRODUCTION', production.toBoolean())
}

task copyConfigs {
    doLast {
        copy {
            from("$projectDir/configs")
            into("$buildDir")
        }
    }
}

tasks.whenTaskAdded { task ->
    if (task.name == 'npm-configure') {
        task.dependsOn(copyConfigs)
    }
}
